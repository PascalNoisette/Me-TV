# -*- mode:python; coding:utf-8; -*-

#  Me TV — A DVB-T player.
#
#  Copyright © 2014 Russel Winder
#
#  This program is free software: you can redistribute it and/or modify it under the terms of the GNU
#  General Public License as published by the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even
#  the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
#  License for more details.
#
#  You should have received a copy of the GNU General Public License along with this program.  If not, see
#  <http://www.gnu.org/licenses/>.
#
#  Author : Russel Winder <russel@winder.org.uk>

Import('environment', 'main_environment', 'xine_player_environment')

sources = Glob('*.cc')
xine_player = File('me-tv-player-xine.cc')

for player in (xine_player,):
    sources.remove(player)

executables = [
    main_environment.Program('me-tv', sources),
    xine_player_environment.Program(xine_player),
]

environment.Alias('install', [
    environment.Install(environment['BINDIR'], executables),
    environment.Install(environment['DATADIR'] +'/pixmaps/', Glob('*.xpm')),
    environment.Install(environment['DATADIR'] +'/me-tv/glade/', ['me-tv.ui', 'me-tv.xpm']),
])

# The list of object files is needed for creating the test executables, so put the data into the environments.
# You have got to love global mutable data.

main_environment['objects'] = [s.name.replace('.cc', '.o') for s in sources]
xine_player_environment['objects'] = [xine_player.name.replace('.cc', '.o')]

Return('executables')
