# -*- mode:python; coding:utf-8; -*-

#  Me TV — A DVB-T player.
#
#  Copyright © 2014 Russel Winder
#
#  This program is free software: you can redistribute it and/or modify it under the terms of the GNU
#  General Public License as published by the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even
#  the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
#  License for more details.
#
#  You should have received a copy of the GNU General Public License along with this program.  If not, see
#  <http://www.gnu.org/licenses/>.
#
#  Author : Russel Winder <russel@winder.org.uk>

import subprocess

Import('environment')

def PkgCheckModules(context, library, versionSpecification):
    pattern = '{} {}'.format(library, versionSpecification) if versionSpecification else library
    context.Message('Checking for ' + pattern + ' ... ')
    returnCode = subprocess.call(['pkg-config', '--exists', pattern])
    if returnCode == 0:
        context.Result('ok')
        context.env.MergeFlags('!pkg-config --cflags --libs ' + library)
        return True
    context.Result('failed')
    return False

configFileWritten = False

def createConfiguration(environment, dependenciesKey):
    global configFileWritten
    configuration = Configure(environment, custom_tests={'PkgCheckModules': PkgCheckModules}, config_h='config.h')  # , clean=False, help=False)
    failedConfiguration = False
    for library, versionPattern in configuration.env[dependenciesKey]:
        if not configuration.PkgCheckModules(library, versionPattern):
            failedConfiguration = True
    if failedConfiguration:
        Exit(1)
    for entry in ['VERSION', 'GETTEXT_PACKAGE', 'PACKAGE_NAME', 'PACKAGE_DATA_DIR', 'PACKAGE_LOCALE_DIR']:
        if not configFileWritten:
            configuration.Define(entry, '"' + environment[entry] + '"')
        configuration.env.Append(CPPDEFINES='{}=\\"{}\\"'.format(entry, environment[entry]))
    if not configFileWritten: configuration.Define('ENABLE_NLS', 1)
    configFileWritten = True
    configuration.env.Append(CPPDEFINES='HAVE_CONFIG_H')
    configuration.env.Append(CPPPATH='.')  # This is crucial for triggering entering config.h in the DAG.
    return configuration.Finish()

mainEnvironment = createConfiguration(environment.Clone(), 'main_dependencies')
xinePlayerEnvironment = createConfiguration(environment.Clone(), 'xine_player_dependencies')

sources = Glob('*.cc')
xinePlayer = File('me-tv-player-xine.cc')

for player in (xinePlayer,): sources.remove(player)

metv = mainEnvironment.Program('me-tv', sources)
metvXinePlayer = xinePlayerEnvironment.Program(xinePlayer)

environment.Alias('install', [
    environment.Install(environment['BINDIR'], [metv, metvXinePlayer]),
    environment.Install(environment['DATADIR'] +'/pixmaps/', Glob('*.xpm')),
    environment.Install(environment['DATADIR'] +'/me-tv/glade/', ['me-tv.ui', 'me-tv.xpm']),
])

Return('metv', 'metvXinePlayer')
